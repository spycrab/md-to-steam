#!/usr/bin/perl
# md-to-steam.pl (c) spycrab0, 2017
# A script for converting GitHub Markdown to the format used by the Steam Workshop
# Licensed under a MIT License. See LICENSE

# TODO:
# Add link support
# Add image support
# Add file option
# Add quiet option
# Add nested list (indented list) support

use strict;
use warnings;
use v5.10;

my ($ulist,$olist,$quote,$code) = (0,0,0,0);

while (<>)
{
    # both
    if (m/```.*?/)
    {
        say '[code]' if (not $code);
        say '[/code]' if ($code);
        $code = !$code;
    }
    
    # end tags
    unless (m/^> /)
    {
        say '[/quote]' if ($quote);
        $quote = 0;
    }

    unless (m/^ *\* /)
    {
        say '[/list]' if ($ulist);
        $ulist = 0;
    }

    unless (m/^ *[0-9]+\. /)
    {
        say '[/olist]' if ($olist);
        $olist = 0;
    }

    # begin tags
    if (m/^ *\* /)
    {
        say '[list]' if (not $ulist);
        $ulist = 1;
    }

    if (m/^ *[0-9]+\. /)
    {
        say '[olist]' if (not $olist);
        $olist = 1;
    }

    if (m/^> /)
    {
        say '[quote]' if (not $quote);
        $quote = 1;
    }

    s/^ *\*/[\*]/; # List items
    s/^ *[0-9]+\. (.*)/[\*] $1/; # Ordered list items
    s/^> //; # Quote markers
    s/^#+ (.*)/\[h1\]$1\[\/h1\]/; # Headings
    s/(?!\[)[\*_][\*_](.*?)[\*_][\*_]/\[b\]$1\[\/b\]/; # Bold text
    s/(?!\[)[\*_](.*?)[\*_]/\[i\]$1\[\/i\]/; # Italic text
    s/(?!``)`(.*?)`/\[i\]$1\[\/i\]/; # There are no inline codetags in steam's markup so I use italic instead
    s/~~(.*?)~~/\[strike\]$1\[\/strike\]/; # Strike out

    print;
}

# Close open tags
say '[/list]' if ($ulist);
say '[/olist]' if ($olist);
say '[/quote]' if ($quote);
say '[/code]' if ($code);

# Print acknowledgement (remove this if you want to, I don't care)
print "\n[i]Generated by md_to_steam.pl from Github Markdown[/i]\n"; 
